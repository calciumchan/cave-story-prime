actor csp_e_igor{
	monster
	health 300
	radius 30
	height 58
	speed 14
	gravity 0.5
	MaxDropOffHeight 999
	damagetype "enemy"
	damagefactor "enemy", 0
	-NODROPOFF
	+JUMPDOWN
	+LOOKALLAROUND
	+noblood
	+shootable
	states{
		spawn:
			igor a 1 a_look
			loop
		see:
			igor abac 4 a_chase
			loop
		melee:
			igor l 16
			tnt1 a 0 A_CustomMeleeAttack((2))
			igor m 8
			goto see
		missile:
			tnt1 a 0 a_jumpif(health > 150,"jump")
			tnt1 a 0 a_jump(10,"jump")
		fireprojectiles:
			tnt1 a 0 a_changevelocity(0,0,0,CVF_RELATIVE|CVF_REPLACE )
			igor f 32
			igor fgfgfgfgfgfgfgfgfgfgfgfgfgfgfgfg 1 bright
			tnt1 a 0 a_giveinventory("csp_igor_firecount",16)
		fireloop:
			igor f 1 bright
			igor g 1 a_custommissile("csp_pr_igor",16,0,random(-8,8),CMF_ABSOLUTEPITCH,random(-2,8))
			tnt1 a 0 a_takeinventory("csp_igor_firecount",1)
			tnt1 a 0 a_jumpifinventory("csp_igor_firecount",1,"fireloop")
			goto see
		jump:
			tnt1 a 0 a_facetarget
			tnt1 a 0 a_changevelocity(10,0,5,CVF_RELATIVE|CVF_REPLACE )
		airtime:
			tnt1 a 0 a_custommissile("csp_pr_igor_hitbox",0)
			igor d 1 a_jumpif(velz==0,"floorcheck")
			loop
		floorcheck:
			igor d 1
			tnt1 a 0 a_custommissile("csp_pr_igor_hitbox",0)
			tnt1 a 0 a_jumpif(velz==0,"land")
			goto airtime
		land:
			tnt1 a 0 acs_namedexecute("csp_spawn_death",0,5,1,8)
			igor e 4 a_changevelocity(0,0,0,CVF_RELATIVE|CVF_REPLACE )
			goto see
		death:
			tnt1 a 0 a_giveinventory("csp_igor_firecount",10)
		deathloop1:
			igor h 2
			tnt1 a 0 acs_namedexecute("csp_spawn_death",0,5,1,8)
			tnt1 a 0 a_takeinventory("csp_igor_firecount",1)
			tnt1 a 0 a_jumpifinventory("csp_igor_firecount",1,"deathloop1")
			tnt1 a 0 a_giveinventory("csp_igor_firecount",10)
		deathloop2:
			igor i 1 
			igor h 1 acs_namedexecute("csp_spawn_death",0,5,1,8)
			tnt1 a 0 a_takeinventory("csp_igor_firecount",1)
			tnt1 a 0 a_jumpifinventory("csp_igor_firecount",1,"deathloop2")
			igor j 32
			igor k -1
			stop
	}
}

actor csp_igor_firecount : csp_counter{}

actor csp_pr_igor{
	projectile
	health 1
	damage (2)
	speed 16
	radius 16
	damagetype "enemy"
	damagefactor "enemy", 0
	+forcexybillboard
	+shootable
	+DONTHARMSPECIES
	+DONTHARMCLASS
	+noblood
	+NOINFIGHTING
	states{
		spawn:
			igpr abc 2 bright light("l_polarstar2")
			igpr abc 2 bright light("l_polarstar2")
			tnt1 a 0 a_changeflag("NOBLOCKMAP",false)
		loopout:
			igpr abc 2 bright light("l_polarstar2")
			loop
		death:
			stop
	}
}

actor csp_pr_igor_hitbox{
	radius 30
	height 58
	damage (1)
	+nogravity
	+missile
	states{
		spawn:
			tnt1 a 2 nodelay
			stop
	}
}
#library "exp_give"
#include "zcommon.acs"

script "csp_give_exp" (int amount){
	str weapon = GetWeapon();
	int level = checkinventory(StrParam(s:weapon, s:"_level"));
	//make sure at least level 1
	if(level == 0){
		giveinventory(StrParam(s:weapon, s:"_level"),1);
		level++;
	}
	//give exp
	while(amount>0){
		//check if levelup
		if(checkinventory(StrParam(s:weapon, s:"_exp_", i:level))
			>=
			getammocapacity(StrParam(s:weapon, s:"_exp_", i:level))
			&&
			level
			<
			getammocapacity(StrParam(s:weapon, s:"_level"))
		){
			level++;
			giveinventory(StrParam(s:weapon, s:"_level"),1);
		}
		//give
		giveinventory(StrParam(s:weapon, s:"_exp_", i:level),1);
		amount--;
	}
	
	//update the display EXP
	ACS_NamedExecute("csp_set_display_exp",0);
}

script "csp_hitcheck" enter{
	while(1==1){
		if(checkinventory("health")<checkinventory("csp_previous_health")){
			acs_namedexecute("csp_take_exp",0,checkinventory("csp_previous_health")-checkinventory("health"));
		}
		takeinventory("csp_previous_health",999);
		giveinventory("csp_previous_health",checkinventory("health"));
		delay(1);
	}
}

script "csp_take_exp" (int amount){
		str weapon = GetWeapon();
	int level = checkinventory(StrParam(s:weapon, s:"_level"));
	//make sure at least level 1
	if(level == 0){
		giveinventory(StrParam(s:weapon, s:"_level"),1);
		level++;
	}
	//give exp
	while(amount>0){
		//check if level down
		if(checkinventory(StrParam(s:weapon, s:"_exp_", i:level))
			<=
			0
			&&
			level
			>
			1
		){
			level--;
			takeinventory(StrParam(s:weapon, s:"_level"),1);
		}
		//take
		takeinventory(StrParam(s:weapon, s:"_exp_", i:level),1);
		amount--;
	}
	
	//update the display EXP
	ACS_NamedExecute("csp_set_display_exp",0);
}

script "csp_set_display_exp" (void){
	str weapon = GetWeapon();
	int level = checkinventory(StrParam(s:weapon, s:"_level"));
	//set the max amount
	setammocapacity("csp_exp_display",getammocapacity(StrParam(s:weapon, s:"_exp_", i:level)) );
	//give the amount you have
	takeinventory("csp_exp_display",999);
	giveinventory("csp_exp_display",checkinventory(StrParam(s:weapon, s:"_exp_", i:level)));
	//level time
	setammocapacity("csp_level_display",getammocapacity(StrParam(s:weapon, s:"_level")) );
	takeinventory("csp_level_display",999);
	giveinventory("csp_level_display",checkinventory(StrParam(s:weapon, s:"_level")));
	//max level
	takeinventory("csp_max_display",999);
	if(checkinventory(StrParam(s:weapon, s:"_level"))==getammocapacity(StrParam(s:weapon, s:"_level")) 
		&&
		checkinventory(StrParam(s:weapon, s:"_exp_", i:level))==getammocapacity(StrParam(s:weapon, s:"_exp_", i:level))
	){
		giveinventory("csp_max_display",1);
	}
}

script "csp_exp_spawn" (int amount){
	int spawnx = getactorx(0);
	int spawny = getactory(0);
	int spawnz = getactorz(0)+4s;
	while(amount>=20){
		spawnforced("csp_exp_20",spawnx,spawny,spawnz);
		amount -= 20;
	}
	while(amount>=5){
		spawnforced("csp_exp_5",spawnx,spawny,spawnz);
		amount -= 5;
	}
	while(amount>0){
		spawnforced("csp_exp_1",spawnx,spawny,spawnz);
		amount --;
	}
}